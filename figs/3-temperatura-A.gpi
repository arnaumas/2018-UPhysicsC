set terminal cairolatex pdf colortext color size 7.8cm, 6cm header '\newcommand{\etiqueta}[1]{\setlength{\fboxsep}{0.75pt}\colorbox{white}{#1}}'
set output '3-temperatura-A.tex'

# Format dels eixos
set xlabel '$\mathsfit t \ (\si{days})$'
set ylabel '$\mathsfit T \ (\si{\celsius})$'
set format '\num{%g}'
set border linewidth 1.5

set style line 1 linecolor rgb "dark-violet" pointtype 1 pointsize 0.01
set style line 2 linecolor rgb "gray" linewidth 1 dashtype 2
set style line 3 linecolor rgb "dark-green" linewidth 1.5 dashtype 4

set xrange [0:31]
set yrange [10:65]
set xtics scale 1,0.5
set mxtics 2
set ytics scale 1,0.5
set mytics 2
set grid xtics ytics mxtics mytics linestyle 2

set label at 25,57 '\etiqueta{\footnotesize $\mathsfit{A} = \SI{1}{m^2}$}' center rotate by -6 front textcolor rgb "dark-violet"
set label at 20,50 '\etiqueta{\footnotesize $\mathsfit{A} = \SI{10}{m^2}$}' center rotate by -13 front textcolor rgb "dark-violet"

plot 39 notitle linestyle 3, 61 notitle linestyle 3, for [i = 0:9] '../codis/3-temperatura-'.i.'.dat' using ($1/24):2 notitle with points linestyle 1
